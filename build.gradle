plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

allure {
    report {
        version.set("2.19.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) // обработка аннотации @Step
        frameworks {
            junit5 { // название фреймворка
                adapterVersion.set("2.19.0") // версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation (
            'io.qameta.allure:allure-selenide:2.29.1',
            'io.qameta.allure:allure-junit5:2.29.1',
            'com.codeborne:selenide:7.11.1',
            'org.junit.jupiter:junit-jupiter:5.10.0',
            'org.slf4j:slf4j-simple:2.0.7'
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    systemProperty "file.encoding", "UTF-8"
    systemProperty "sun.jnu.encoding", "UTF-8"
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties System.getProperties() // Передаем все системные свойства в тесты

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

// Задача для запуска тестов с определенными тегами
task smoke_tests(type: Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }

    // Очищаем результаты перед запуском
    doFirst {
        delete 'build/allure-results'
    }
}

task regression_tests(type: Test) {
    useJUnitPlatform {
        includeTags("regression")
    }

    doFirst {
        delete 'build/allure-results'
    }
}